<file> ::= {<file-element>}*

<file-element> ::= <static-declaration>
                 | <record-declaration>
                 | <function-declaration>

<static-declaration> ::= static [extern | export] <declaration> [= <initializer>] ;

<record-declaration> ::= record <identifier> { [<declaration-list>] } ;

<function-declaration> ::= fn [extern | export] <identifier> ( [<parameters>] ) [-> <type-declaration>] <function-body-or-semicolon>

<parameters> ::= <declaration-list>
               | <declaration-list> , ...
               | ...

<function-body-or-semicolon> ::= ;
                               | { {<statement>}* }

<declaration-list> ::= <declaration>
                     | <declaration-list> , <declaration>

<declaration> ::= <identifier> : <type-declaration>

<initializer> ::= <expression>
                | { <initializer-list> }

<initializer-list> ::= <initializer>
                     | <initializer-list> , <initializer>

<type-declaration> ::= bool
                     | u8
                     | i8
                     | u16
                     | i16
                     | u32
                     | i32
                     | u64
                     | i64
                     | <identifier>
                     | * <type-declaration>
                     | [ <type-declaration> ; <expression> ]
                     | fn ( <type-list> ) [-> <type-declaration>]

<type-list> ::= <type-declaration>
              | <type-list> , <type-declaration>

<statement> ::= <identifier> :
              | <conditional-jump> <label> , <expression> , <expression> ;
              | jmp <identifier> ;
              | auto <identifier> [: <type-declaration>] [= <initializer>] ;
              | eval <expression> ;
              | set <expression> , <expression> ;
              | ret [<expression>] ;

<conditional-jump> ::= jeq
                     | jneq
                     | jl
                     | jle
                     | jg
                     | jge

<expression> ::= <lor-expression>

<lor-expression> ::= <land-expression>
                   | <lor-expression> || <land-expression>

<land-expression> ::= <or-expression>
                    | <land-expression> && <or-expression>

<or-expression> ::= <xor-expression>
                  | <or-expression> | <xor-expression>

<xor-expression> ::= <and-expression>
                   | <xor-expression> ^ <and-expression>

<and-expression> ::= <eq-expression>
                   | <and-expression> & <rel-expression>

<eq-expression> ::= <rel-expression>
                  | <eq-expression> == <rel-expression>
                  | <eq-expression> != <rel-expression>

<rel-expression> ::= <shift-expression>
                   | <rel-expression> <  <shift-expression>
                   | <rel-expression> <= <shift-expression>
                   | <rel-expression> >  <shift-expression>
                   | <rel-expression> >= <shift-expression>

<shift-expression> ::= <additive-expression>
                     | <shift-expression> << <additive-expression>
                     | <shift-expression> >> <additive-expression>

<additive-expression> ::= <multiplicative-expression>
                        | <additive-expression> + <multiplicative-expression>
                        | <additive-expression> - <multiplicative-expression>

<multiplicative-expression> ::= <cast-expression>
                              | <multiplicative-expression> * <cast-expression>
                              | <multiplicative-expression> / <cast-expression>
                              | <multiplicative-expression> % <cast-expression>

<cast-expression> ::= <unary-expression>
                    | <cast-expression> as <type-declaration>

<unary-expression> ::= <postfix-expression>
                     | + <unary-expression>
                     | - <unary-expression>
                     | ~ <unary-expression>
                     | ! <unary-expression>
                     | & <unary-expression>
                     | * <unary-expression>

<postfix-expression> ::= <primary-expression>
                       | <postfix-expression> ( [<expression-list>] )
                       | <postfix-expression> [ <expression> ]
                       | <postfix-expression> . <identifier>

<primary-expression> ::= ( <expression> )
                       | <string-literal> [<type-suffix>]
                       | <integer-constant> [<type-suffix>]
                       | <char-constant> [<type-suffix>]
                       | <bool-constant>
                       | <identifier>

<type-suffix> ::= u8
                | i8
                | u16
                | i16
                | u32
                | i32
                | u64
                | i64

<bool-constant> ::= true
                  | false

<expression-list> ::= <expression>
                    | <expression-list> , <expression>
